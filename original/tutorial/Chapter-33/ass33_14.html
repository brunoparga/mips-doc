<!doctype html> 
<html lang="en">

<!-- LessonMaker.java, version June 3, 2018 on Sat May 29 09:44:06 EDT 2021 -->


<!-- Mirrored from chortle.ccsu.edu/assemblytutorial/Chapter-33/ass33_14.html by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 20 Jul 2023 11:09:31 GMT -->
<head>
<meta charset="utf-8">
<meta name="author" content="Bradley Kjell kjell at ieee dot org">
<meta name="robots" content="index,follow">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<title>Complete Program</title>
<link rel="stylesheet" href="../AssemblyStyleRev3.css">
<script src="../CAIscripts.js"></script>
</head>

<body>


<!-- TOP NAVIGATION DIVISION -->
<div class="topnavigation">
<a href="ass33_13.html"><img src="../backIcon.gif" alt="go to previous page"></a> &nbsp; 
<a href="../index-2.html#part9"><img src="../homeIcon.gif" alt="go to home page"></a> &nbsp;
<a href="ass33_15.html"><img src="../nextIcon.gif" alt="go to next page"></a>
<a><img src='../audioIcon.gif' alt='hear noise' onclick='doAudio()'></a>
<audio id='player' src='../../external.html?link=https://chortle.ccsu.edu/assemblytutorial/Audio/chap33-14.mp3'></audio>
<a><img src='../highLightIcon.gif' alt='highlighting' onclick='doHighlight()' style="display: block;float:right"></a>
</div>

<!-- ANSWER DIVISION -->
<div class="answer">

<h3>Answer:</h3>
<p>
Of course!
</p>
<br class="clearfloats"> 
</div>

<!-- LESSON DIVISION -->
<div class="lesson">
<h1>Complete Program</h1>
<!-- ../Audio/chap33-14.mp3 -->


<div class="left">
<p>
<span class="highlight">Here
is a complete version of the program.</span>
It creates and initializes a struct.
Then it creates a second struct and copies
the first struct's data to it.
Finally it prints out the data in both structs
using a subroutine.
</p>
<p>
The subroutine has been extended so that all fields are printed.
The argument is the address of a struct.
</p>
</div>

<div class="right">
<p>
Copy the entire program to a file <code>StructPrint.asm</code> and run it with SPIM.
</p>

<p>
Or, if you want,
copy <code>main</code> to one file and <code>PStruct</code> to another file
and load them separately with SPIM.
</p>

</div>
<br style="clear:both"> 

<pre class="program">
# StructPrint.asm
#
# Allocate memory for a struct and then initialize it.
# Allocate memory for a second struct and copy data
# from the first into it. Print both structs

        .text
        .globl  main

main:    
        # create the first struct 
        li      $v0,9             # allocate memory
        li      $a0,12            # 12 bytes
        syscall                   # $v0 &lt;-- address
        move    $s1,$v0           # $s1 first struct
 
        # initialize the first struct       
        li      $t0,34            # store 34
        sw      $t0,0($s1)        # in age
        lw      $t0,pay           # store 24000
        sw      $t0,4($s1)        # in pay
        li      $t0,12            # store 12
        sw      $t0,8($s1)        # in class

        # create the second struct 
        li      $v0,9             # allocate memory
        li      $a0,12            # 12 bytes
        syscall                   # $v0 &lt;-- address
        move    $s2,$v0           # $s2 second struct
           
        # copy data from first struct to second     
        lw      $t0,0($s1)        # copy age from first
        sw      $t0,0($s2)        # to second struct
        lw      $t0,4($s1)        # copy pay from first
        sw      $t0,4($s2)        # to second struct
        lw      $t0,8($s1)        # copy class from first
        sw      $t0,8($s2)        # to second struct
           
        # write out the first struct    
        move    $a0,$s1
        jal     PStruct
           
        # write out the second struct    
        move    $a0,$s2
        jal     PStruct
        
        li      $v0,10            # return to OS
        syscall       

        .data
pay:    .word   24000             # rate of pay, in static memory

        .text
# Subroutine PStruct: print a struct
#
# Registers: $a0 --- address of the struct
#            $ra --- return address
PStruct:
        sub     $sp,$sp,4         # push $s0
        sw      $s0,($sp)         # onto the stack
        
        move    $s0,$a0           # safe copy of struct address
        
        la      $a0,agest         # print "age:"
        li      $v0,4
        syscall
        lw      $a0,0($s0)        # print age
        li      $v0,1
        syscall

        la      $a0,payst         # print " pay: "
        li      $v0,4
        syscall
        lw      $a0,4($s0)        # print pay
        li      $v0,1
        syscall

        la      $a0,classt         # print " class: "
        li      $v0,4
        syscall
        lw      $a0,8($s0)        # print class
        li      $v0,1
        syscall

        la      $a0,lf            # end the line
        li      $v0,4
        syscall

        add     $sp,$sp,4         # restore $s0 of caller 
        lw      $s0,($sp)
        jr      $ra               # return to caller        

        .data
agest:  .asciiz "age:   "
payst:  .asciiz "  pay:   "
classt: .asciiz "  class: "
lf:     .asciiz "\n"

## end of file
</pre>


<br class="clearfloats"> 
</div> <!-- end LESSON -->

<!-- QUESTION DIVISION -->
<div class="question">
<h3> QUESTION 14:</h3>
<p>
How is a memory allocation request done in C ?
</p>
<br class="clearfloats"> 
</div> <!-- end QUESTION -->

<!-- BOTTOM NAVIGATION DIVISION -->
<div class="bottomnavigation">
<a href="ass33_13.html"><img src="../backIcon.gif" alt="go to previous page"></a> &nbsp; 
<a href="../index-2.html#part9"><img src="../homeIcon.gif" alt="go to home page"></a> &nbsp;
<a href="ass33_15.html"><img src="../nextIcon.gif" alt="go to next page"></a>
</div>

</body>

<!-- Mirrored from chortle.ccsu.edu/assemblytutorial/Chapter-33/ass33_14.html by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 20 Jul 2023 11:09:32 GMT -->
</html>
